# Sublime Text allows "//" and "/* */" style comments as well as extra commas, so isn't "pure" json

JsonFile       <-    Values EndOfFile?
Values         <-    Spacing? Value Spacing? (',' Spacing? Value Spacing?)* JunkComma
Value          <-    (Dictionary / Array / QuotedText / Float / Integer / Boolean / Null)
Null           <-    "null"
Dictionary     <-    '{' KeyValuePairs* Spacing? '}'
Array          <-    '[' Values* Spacing? ']'
KeyValuePairs  <-    Spacing? KeyValuePair Spacing? (',' Spacing? KeyValuePair Spacing?)*
KeyValuePair   <-    QuotedText ':' Spacing? Value
QuotedText     <-    '"' Text? '"'
Text           <-    &'"' / ('\\' . / (!'"' .))+
Integer        <-    '-'? '0' ![0-9] / '-'? [1-9] [0-9]*
Float          <-    '-'? [0-9]* '.' [0-9]+ ([Ee] [-+]? [0-9]*)? / '-'? [0-9]+ [Ee] [-+]? [0-9]+
Boolean        <-    "true" / "false"
Spacing        <-    (Comment / [ \t\n\r])+
EndOfFile      <-    !.

JunkComma      <-    (',' Spacing)?
Comment        <-    LineComment / BlockComment
LineComment    <-    "//" (![\n\r] .)* [\n\r]
BlockComment   <-    "/*" (!"*/" .)* "*/"
